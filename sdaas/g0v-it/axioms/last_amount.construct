##
## AXIOM: create an api level account budget
##
PREFIX g0v: <http://data.budget.g0v.it/g0v-budget/v1#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX : <urn:local:g0v:api:v1:>

CONSTRUCT { 
	?account :last_amount ?last_amount
} WHERE {  
    
    {
		SELECT ?code_history ( MAX (?year) AS ?last_year)
		WHERE {
		  ?account_record a :AccountRecord;
				:code ?code_history ;
				:year ?year             
		} GROUP BY ?code_history 
	}
    
  	?account a :Account;
               :code ?code.
  
  	?account_record a :AccountRecord; 
                      :code ?code_history; 
                      :year ?last_year; 
                      :amount ?last_amount.
	
	FILTER(?code = ?code_history )
}