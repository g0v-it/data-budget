@prefix dcat: <http://www.w3.org/ns/dcat#> .
@prefix foaf: <http://xmlns.com/foaf/0.1/> .
@prefix xsd: <http://www.w3.org/2001/XMLSchema#> .
@prefix void: <http://rdfs.org/ns/void#> .
@prefix prov: <http://www.w3.org/ns/prov#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix kees: <http://linkeddata.center/kees/v1#> .
@prefix sd: <http://www.w3.org/ns/sparql-service-description#> .
@prefix dct: <http://purl.org/dc/terms/> .
@prefix : <#> .
	
:table_introspection_catalogue a kees:Table ;
	dct:identifier "introspection:catalogue" ;
	rdfs:label "Knowledge base catalog"@en ;
	rdfs:comment "All RDFtriples by graphs"@en ;
	kees:queryText """# All RDFtriples by graphs
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX kees: <http://linkeddata.center/kees/v1#>

SELECT ?type ?title ?publisher ?license ( ?graph AS ?graph_name) ?trust ?size
WHERE {  

  {
    SELECT ?graph (COUNT(*) AS ?size)  
    WHERE {  
      GRAPH ?graph {?s ?p ?o} 
    } GROUP BY ?graph 
  }
  	
  OPTIONAL { [] sd:name ?graph; a ?ut} 
  BIND( COALESCE(?ut, sd:NamedGraph) AS ?uriType )
  VALUES (?uriType ?type) {
    (kees:LinkedDataGraph "A - dataset")
    (kees:InferredKnowledgeGraph "B - inference")
    (kees:TBoxGraph "C - vocabulary")
    (kees:KBConfigGraph "D - config")
    (sd:NamedGraph "Generic named graph")
  }

  OPTIONAL { [] sd:name ?graph; kees:trust ?trust }
  
  OPTIONAL {
    [] sd:name ?graph; dct:source ?source. 
    ?ds  dct:title ?dataset_title ; dcat:distribution ?dist.
    ?dist dcat:accessURL ?source. 
    OPTIONAL{ ?dist dct:license ?licenseURL } 
    OPTIONAL{ ?licenseURL dct:title ?license_title }  
    OPTIONAL { ?ds dct:publisher [ foaf:name ?ds_publisher] } 
  }
  OPTIONAL { [] a kees:KnowledgeBase; dct:publisher [ foaf:name ?kb_publisher] }
  
  OPTIONAL {
    [] a kees:InferredKnowledgeGraph; sd:name ?graph; dct:title ?graph_title . 
    BIND ( "SDaaS Agent" as ?agent ) 
    BIND ( "N.A." as ?license_title ) 
  }
  BIND( COALESCE(?graph_title, ?dataset_title) AS ?title ) 
  BIND( COALESCE(?license_title, ?licenseURL, "not provided") AS ?license ) 
  BIND( COALESCE(?ds_publisher, ?agent, ?kb_publisher) AS ?publisher ) 

} ORDER BY ?type ?publisher DESC(?size)
""" 
.
