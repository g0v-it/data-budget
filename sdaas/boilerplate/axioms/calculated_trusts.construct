#
# This axiom calulate the mean value for graph trust
#
# Copyright (c) 2017 by LinkedData.Center. Some right reserved, refer to project license
#
PREFIX kees: <http://linkeddata.center/kees/v1#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX daq: <http://purl.org/eis/vocab/daq#> 
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>

CONSTRUCT {
	?graphUri sd:name ?g; kees:trust ?trust 
} WHERE {	
	{
		SELECT ?g (AVG(?value) AS ?trust)
		WHERE {
		  GRAPH ?g {}
		  ?observation a qb:Observation ;
		  daq:metric kees:trustGraphMetric;
		  daq:computedOn ?g ;
		  daq:value ?value
		} GROUP BY ?g
	}
	BIND ( IRI(CONCAT("urn:graph:",MD5(STR(?g)))) AS ?graphUri)
	FILTER NOT EXISTS { [] sd:name ?g; kees:trust [] }
}