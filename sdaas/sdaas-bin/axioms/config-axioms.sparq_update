#
# Infers missing knowledge base configuration information
#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX kees: <http://linkeddata.center/kees/v1#> 
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#> 
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX sdaas: <http://linkeddata.center/sdaas/v1#> 
PREFIX graph: <urn:local:g0v:budget:it:graph:> 


INSERT DATA {
	GRAPH <urn:sdaas:config> {
		<urn:sdaas:config:kb> a sd:Service;
			sd:defaultDataset <urn:sdaas:config:kb:default>;
		    sd:supportedLanguage sd:SPARQL11Query, sd:SPARQL11Update ;
		    sd:feature sd:UnionDefaultGraph;
    		sd:resultFormat <http://www.w3.org/ns/formats/RDF_XML>, <http://www.w3.org/ns/formats/Turtle> ;
    		sd:defaultDataset <urn:sdaas:config:kb:default>.
    		
    	<urn:sdaas:config:kb:default> a sd:Dataset
    }
}

;

####################################
# SPARQL service structure inference
####################################
WITH <urn:sdaas:config>
INSERT {
	?namedGraph a sd:NamedGraph.
	<urn:sdaas:config:kb:default> sd:namedGraph ?namedGraph.
}
WHERE {
	{ ?namedGraph a kees:KBConfigGraph }
	UNION
	{ ?namedGraph a kees:LinkedDataGraph }
	UNION
	{ ?namedGraph a kees:InferredKnowledgeGraph }
}


