##
## APPLICATION LEVEL AXIOM: infers history records for all accounts
##
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX g0v: <http://data.budget.g0v.it/g0v-ap/v1#>
PREFIX : <http://data.budget.g0v.it/g0v-ap-api/v1#>

CONSTRUCT { ?component :hasHistoryRec ?history_rec } 
WHERE {
	?component a :Account; 
		:refPeriod ?ref_period; 
		g0v:concept/skos:closeMatch ?close_match.
	?history_rec a :Account; 
		:refPeriod ?history_ref_period ; 
		g0v:concept ?close_match.
  
    FILTER( ?component != ?history_rec )
    FILTER( STR(?history_ref_period) < STR(?ref_period) )
    FILTER NOT EXISTS { ?component :hasHistoryRec ?history_rec }
}