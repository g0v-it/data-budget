#
# Default metadata for BubbleGraph
#
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bgo: <http://linkeddata.center/lodmap-bgo/v1#>
PREFIX accounts: <https://data.budget.g0v.it/ldp/accounts#>
PREFIX resource: <http://mef.linkeddata.cloud/resource/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX fr: <http://linkeddata.center/botk-fr/v1#>

#
# Set  Bubble Graph title from source dataset
#
INSERT { ?bgo dct:title ?title } 
WHERE {
 	?bgo a bgo:BubbleGraph ; dct:source/dct:title ?title . 
 	
	FILTER NOT EXISTS { ?bgo dct:title  []}
}

;

#
# Set  Bubble Graph description from source dataset
#
INSERT { ?bgo dct:description ?description } 
WHERE {
 	?bgo a bgo:BubbleGraph ; dct:source/dct:description ?description . 
 	
	FILTER NOT EXISTS { ?bgo dct:description  []}
}

;


#
# Create <accounts#ministero> partition schema
#
INSERT {
  accounts:ministero bgo:partition ?partitionUri.
  ?partitionUri bgo:label ?partitionLabel; bgo:partitionAmount ?partitionAmount.
} WHERE {
  {
    SELECT DISTINCT ?partitionLabel ( SUM(?amount) AS ?partitionAmount) WHERE  {
		?bubble a bgo:Account ; 
	      	dct:source/fr:concept/skos:inScheme ?taxonomy ; 
	      	bgo:partitionLabel ?partitionLabel ;
	      	bgo:amount ?amount .
      
		?taxonomy skos:hasTopConcept/skos:prefLabel ?partitionLabel
	} GROUP BY ?partitionLabel
  }
  BIND( IRI( CONCAT( "https://data.budget.g0v.it/ldp/accounts#ministero_",MD5(STR(?partitionLabel)))) AS ?partitionUri )
}


;


#
# Create <accounts#missione> partition schema
#
INSERT {
  accounts:missione bgo:partition ?partitionUri.
  ?partitionUri bgo:label ?partitionLabel; bgo:partitionAmount ?partitionAmount.
} WHERE {
  {
    SELECT DISTINCT ?partitionLabel ( SUM(?amount) AS ?partitionAmount) WHERE  {
		?bubble a bgo:Account ; 
	      	dct:source/fr:concept/skos:inScheme ?taxonomy ; 
	      	bgo:partitionLabel ?partitionLabel ;
	      	bgo:amount ?amount .
      
		?taxonomy skos:hasTopConcept/skos:narrower/skos:prefLabel ?partitionLabel
	} GROUP BY ?partitionLabel
  }
  BIND( IRI( CONCAT( "https://data.budget.g0v.it/ldp/accounts#missione_",MD5(STR(?partitionLabel)))) AS ?partitionUri )
}


