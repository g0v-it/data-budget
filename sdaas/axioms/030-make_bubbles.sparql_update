#
# Create bubbles from a Financial Report Component with theme dcat:theme resource:ldb_theme
#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX fr: <http://linkeddata.center/botk-fr/v1#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#> 
PREFIX interval: <http://reference.data.gov.uk/def/intervals/>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX bgo: <http://linkeddata.center/lodmap-bgo/v1#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>


INSERT { 
   ?bubbleUri a bgo:Account ;
		bgo:inBubbleGraph ?bgo ;
		bgo:code ?code ;
		dct:title ?title ;
		dct:subject ?subject ;
		dct:source ?component ;
		bgo:amount ?amount ;
		bgo:version "last"; #?year ;
		bgo:partitionLabel 
			?labelMinistero, 
			?labelMissione
} 
WHERE {

	?bgo a bgo:BubbleGraph ; dct:source ?fr .
  	?fr a fr:FinancialReport.
  	

	?component a fr:Component ;
		qb:dataSet ?fr ;
		fr:concept ?azione ;
		fr:refPeriod/time:hasBeginning/interval:ordinalYear ?year ;
		fr:amount ?amount 
	.
	

  	# Il titolo della bolla è la prefLabel della azione.
	?azione skos:prefLabel ?title .
	
	# Il subject è il programma della azione 
	?azione skos:broader ?programma . 
	?programma skos:prefLabel ?subject  . 
		
	# La missione del programma è la seconda partizione
	?programma skos:broader ?missione .
	?missione skos:prefLabel ?labelMissione . 
	
	# Il ministero della missiome è la prima partizione
	?missione skos:broader ?ministero .
  	?ministero skos:prefLabel ?labelMinistero.
  
  
  BIND( MD5(str(?component)) AS ?code)	
  BIND( IRI( CONCAT("https://data.budget.g0v.it/ldp/account/",?code,"#data")) AS ?bubbleUri)
  BIND(?partitionLabel2 AS ?subject)
}

;


#
#  Create comments
#
INSERT { ?bubbleUri dct:description ?description} 
WHERE {
	?bubbleUri a bgo:Account ; dct:source/fr:concept/skos:broader ?programma .
	?programma skos:prefLabel ?label .
	OPTIONAL { ?programma rdfs:comment ?comment }
	BIND( COALESCE(?comment, ?label) AS ?description)
	FILTER NOT EXISTS { ?bubbleUri dct:description [] }
}

;

#
# Create history rec
# N.B. all history records considerd (i.e. also records from different dataset version)
#

INSERT {
	?account bgo:isVersionOf ?historyRec .
	?historyRec a bgo:VersionedAmount ; 
	  	dct:source ?source2 ;
		bgo:version ?version ;
		bgo:amount ?amount
	.
} WHERE {
    ?account a bgo:Account; dct:source ?source1 .
    ?source1 fr:concept ?concept .
    ?source2 fr:concept/skos:closeMatch ?concept .
    FILTER( ?source1 != ?source2 )
  
    ?source1 qb:dataSet ?dataset1.
    ?source2 qb:dataSet ?dataset2.

  	?datase1 dcat:theme ?theme.
    ?datase2 dcat:theme ?theme.

    ?source2 fr:amount ?amount ; 
    	fr:refPeriod/time:hasBeginning/interval:ordinalYear ?version . 
  	BIND( IRI( CONCAT( STR(?source2),"_bh")) AS ?historyRec )
}

;

#
# Create previousValue
# N.B only history records with the same theme (i.e. version) in source datasets are considered 
#
INSERT { ?account bgo:previousValue ?lastAmount }
WHERE {
    ?account a bgo:Account; 
		dct:source/qb:dataSet/dcat:theme ?accountSourceTheme;
		bgo:isVersionOf ?historyRec .
  
	?historyRec bgo:version ?version ;
        bgo:amount ?lastAmount;
        dct:source/qb:dataSet/dcat:theme ?historySourceTheme .
  
    FILTER( ?historySourceTheme = ?accountSourceTheme )
  
    FILTER NOT EXISTS {
		?account bgo:isVersionOf ?historyRec2.
        ?historyRec2 bgo:version ?version2 ;
        	dct:source/qb:dataSet/dcat:theme ?historySourceTheme2 .
    	FILTER( ?historySourceTheme2 = ?accountSourceTheme )
        FILTER (?version2 > ?version)
    }             
}


;

#
# Create account parts
#
INSERT {
  ?account bgo:hasPart ?part .
  ?part a bgo:PartialAmount ; 
  dct:source ?fact ; 
  dct:title ?partTitle ; 
  bgo:amount ?partAmount .
} 
WHERE {
	?account a bgo:Account ; dct:source ?component . 
	?fact a fr:Fact; 
		fr:isPartOf ?component ;
		fr:concept/skos:prefLabel ?partTitle ;
		fr:amount ?partAmount
	.
	BIND( IRI( CONCAT( STR(?fact),"_bp")) AS ?part )
}

