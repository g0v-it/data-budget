##
## SKOS AXIOM: map close concepts
##
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

INSERT { 
	?concept skos:closeMatch ?close_concept
} WHERE {  
	?concept a skos:Concept; 
		skos:notation ?code; 
		skos:inScheme ?scheme.
	
	?close_concept a skos:Concept; 
		skos:notation ?code;
		skos:inScheme ?other_scheme.
	
	FILTER(?concept != ?close_concept )
	FILTER(?scheme != ?other_scheme )
	FILTER NOT EXISTS {?concept skos:closeMatch ?close_concept}
}

;

##
## SKOS AXIOM: infer top concepts
##
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

INSERT { 
	?scheme skos:hasTopConcept ?concept
} WHERE {  
	?concept a skos:Concept; 
		skos:inScheme ?scheme.

	FILTER NOT EXISTS {?concept skos:broader []}
	FILTER NOT EXISTS {?scheme skos:hasTopConcept ?concept}
}

;

##
## SKOS AXIOM: infer narrow concepts
##
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

INSERT { 
	?broader_concept skos:narrow ?narrow_concept
} WHERE {  
	?narrow_concept a skos:Concept.
	?narrow_concept	skos:broader ?broader_concept.

	FILTER NOT EXISTS {?broader_concept skos:narrow ?narrow_concept}
}

