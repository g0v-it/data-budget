########################################################################################
##@ bgo partitions
########################################################################################
PREFIX fr: <http://linkeddata.center/botk-fr/v1#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX qb: <http://purl.org/linked-data/cube#> 
PREFIX mef: <http://w3id.org/g0v/it/mef#> 
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX bgo: <http://linkeddata.center/lodmap-bgo/v1#>
PREFIX : <http://mef.linkeddata.cloud/resource/bgo_> 


##
## Assign partitions to overview
##
INSERT { ?partitions bgo:hasPartition :p1 , :p2 }
WHERE {
	?domain a bgo:Domain; 
		bgo:has:overview/bgo:hasPartitions ?partitions .
}


;

##
## create a partition to store ministero
##
INSERT DATA {
	:p1
		a bgo:Partition ;
		bgo:partitionId "ministero" ;
		bgo:label "Ministero" ;
		bgo:title "Suddivisione per Ministero" 
}

;

##
## create a p1 partition subsets to store ministero
##
INSERT {
	:p1 bgo:hasAccountSubset ?subset .
	?subset a bgo:AccountSet ;
		bgo:title ?title
	.
}
WHERE {
	?ministero a mef:Ministero ; skos:notation ?notation ; skos:definition ?title.
	BIND( IRI(CONCAT(STR(:p1), "_", ?notation)) AS ?subset)
}

;

##
## populate a p1 partition subsets 
##
INSERT { ?subset bgo:hasAccount ?account }
WHERE {
	?account a bgo:Account; dct:source/fr:isPartOf/fr:isPartOf/fr:isPartOf/skos:notation ?notation;
	BIND( IRI(CONCAT(STR(:p1), "_", ?notation)) AS ?subset)
}

;

##
## create a partition to store missione
##
INSERT DATA {
	:p2
		a bgo:Partition;
		bgo:partitionId "missione" ;
		bgo:label "Missione" ;
		bgo:title "Suddivisione per missione" 
	.
}

;

##
## create a p2 partition subsets to store missione
##
INSERT {
	:p2 bgo:hasAccountSubset ?subset .
	?subset a bgo:AccountSet ;
		bgo:title ?title
	.
}
WHERE {
	{
		SELECT DISTINCT ?missioneId ?title WHERE {
			?missione a mef:Missione ; skos:notation ?notation ; skos:definition ?title.
			BIND( STRAFTER(?notation, "-") AS ?missioneId )
		}
	}
	BIND( IRI(CONCAT(STR(:p2), "_", ?missioneId)) AS ?subset)
}

;

##
## populate a p2 partition subsets 
##
INSERT { ?subset bgo:hasAccount ?account }
WHERE {
  	SELECT DISTINCT ?subset ?account WHERE {
      ?account a bgo:Account; dct:source/fr:isPartOf/fr:isPartOf/skos:notation ?notation;
      BIND( IRI(CONCAT(STR(:p2), "_", STRAFTER(?notation, "-"))) AS ?subset)
    }
}


#trust=1.00	