#
# (c)2019 Enrico Fagnoni (enrico at LinkedData.Center) with CC-BY license
#
##############################################################################
##  all component are related to one or more taxonomies exposed by budget
##############################################################################
PREFIX mef: <http://w3id.org/g0v/it/mef#>

CONSTRUCT {?concept mef:inTaxonomy ?scheme }
WHERE  { 
    {       
        ?concept a ?component ; mef:inBudget/mef:hasSchemeSMMPACP ?scheme .
        VALUES ?component {
            mef:Ministero
            mef:MissioneMinistero
            mef:Programma
            mef:Azione
            mef:CapitoloDiSpesa
            mef:PianoDiGestione
        }
    } 
    UNION
    {
        ?concept a ?component; mef:inBudget/mef:hasSchemeSMRMP ?scheme .
        VALUES ?component {
            mef:Ministero
            mef:CentroResponsabilita
            mef:MissioneMinistero
            mef:Programma
        }
    } 
    UNION
    {
        ?concept a ?component; mef:inBudget/mef:hasSchemeSMTC ?scheme  .
        VALUES ?component {
            mef:TitoloSpesa
            mef:CategoriaSpesa
        }
    } 
    UNION
    {
        ?concept a ?component; mef:inBudget/mef:hasSchemeSTCCP ?scheme  .
        VALUES ?component {
            mef:TitoloSpesa
            mef:CategoriaSpesa
            mef:CapitoloDiSpesa
            mef:PianoDiGestione
        }       
    } 
    UNION
    {
        ?concept a ?component; mef:inBudget/mef:hasSchemeSMP ?scheme  .
        VALUES ?component {
            mef:Missione
            mef:Programma
        }       
    } 
    UNION
    {
        ?concept a ?component; mef:inBudget/mef:hasSchemeETNT ?scheme  .
        VALUES ?component {
            mef:TitoloEntrata
            mef:Natura
            mef:Tipologia
        }
    } 
    UNION
    {
        ?concept a ?component; mef:inBudget/mef:hasSchemeETTPCA ?scheme  .
        VALUES ?component {
            mef:TitoloEntrata
            mef:Tipologia
            mef:Provento
            mef:CapitoloEntrata
            mef:Articolo
        }       
    } 
}