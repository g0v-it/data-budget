########################################################################################
##@ inferences using fr-ap-mef axioms
########################################################################################
PREFIX fr: <http://linkeddata.center/botk-fr/v1#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX qb: <http://purl.org/linked-data/cube#> 
PREFIX mef: <http://w3id.org/g0v/it/mef#> 
PREFIX sdmx-attribute:	<http://purl.org/linked-data/sdmx/2009/attribute#> 

INSERT {
	?capitolo a skos:Concept, fr:Fact ;
		skos:broader ?azione ;
		fr:concept ?capitolo
	.
	
	?azione a skos:Concept, fr:Component, mef:Azione ;
		skos:narrower ?capitolo ;
		skos:inScheme ?report;
		qb:dataSet ?report;
		skos:broader ?programma ;
		fr:concept ?azione
	.
	
	?programma a skos:Concept, fr:Component, mef:Programma ;
		skos:narrower ?azione ;
		skos:inScheme ?report;
		qb:dataSet ?report;
		skos:broader ?missione ;
		fr:concept ?programma
	.
	
	?missione a skos:Concept, fr:Component, mef:Missione ;
		skos:narrower ?programma ;
		skos:inScheme ?report;
		qb:dataSet ?report;
		skos:broader ?ministero ;
		fr:concept ?missione
	.
	
	?ministero a skos:Concept, fr:Component, mef:Ministero ;
		skos:narrower ?missione ;
		skos:inScheme ?report;
		qb:dataSet ?report;
		fr:concept ?ministero
	.
	?report a fr:FinancialReport, mef:FinancialReport, skos:ConceptScheme, mef:Budget ;
		skos:hasTopConcept ?ministero
	.
	
}
WHERE {
	?capitolo a mef:Capitolo;
		qb:dataSet ?report ;
		fr:isPartOf ?azione
	.
	?azione fr:isPartOf ?programma .
	?programma fr:isPartOf ?missione .
	?missione fr:isPartOf ?ministero .
}

;

INSERT { ?component fr:amount ?calculatedAmount }
WHERE {
	SELECT ?component (SUM(?amount) as ?calculatedAmount) WHERE {
		?fact a mef:Capitolo ;  
			fr:isPartOf+ ?component  ;
			fr:amount ?amount .		
	} GROUP BY ?component
}

;

# find close concepts. Limit analysis to Azioni to maximize performances
INSERT { ?concept1 skos:closeConcept ?concept1  }
WHERE {
	?concept1 a mef:Azione ; skos:notatation ?notation .
	?concept2 a mef:Azione ; skos:notatation ?notation .
	FILTER( ?concept1 != ?concept2 )
}

#trust=1.00	
