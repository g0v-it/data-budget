########################################################################################
##@ Create bgo accounts with core metadata
########################################################################################
PREFIX fr: <http://linkeddata.center/botk-fr/v1#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX qb: <http://purl.org/linked-data/cube#> 
PREFIX mef: <http://w3id.org/g0v/it/mef#> 
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bgo: <http://linkeddata.center/lodmap-bgo/v1#>


CONSTRUCT { 
	?azione a bgo:Account ;
		bgo:accountId ?accountId ;
		bgo:title ?title ;
		bgo:description ?description ;
		bgo:abstract ?abstract ;
		rdfs:seeAlso ?bubble ;
	.
} WHERE { 
	?azione a mef:Azione; 
		qb:dataSet ?domain ;
		dct:identifier ?accountId ;
		skos:definition ?title ;
		skos:notation ?notation ;
		fr:isPartOf ?programma
	.
	?domain a bgo:Domain .
	
	?programma a mef:Programma ; fr:isPartOf ?missione .
	?missione a mef:MissioneMinistero ; fr:isPartOf ?ministero .
	?ministero a mef:Ministero ; skos:prefLabel ?description .

	BIND( IRI(CONCAT("https://budget.g0v.it/account/",?accountId)) AS ?bubble)
	OPTIONAL { ?azione skos:editorialNote ?azioneEditorialNote }
	OPTIONAL { ?programma  skos:definition ?programmaDefinition }
	OPTIONAL { ?programma skos:editorialNote ?programmaEditorialNote }
	OPTIONAL { ?missione skos:prefLabel ?missioneDescription }
	
	{
		SELECT ?azione (GROUP_CONCAT(?capitoloDescription;separator= "\n- ") AS ?capitoliList) WHERE {
			?azione a mef:Azione ; qb:dataSet ?domain .
			?domain a bgo:Domain .
			?cds a mef:CapitoloDiSpesa ;
				fr:isPartOf ?azione ; 
				skos:notation ?notation .
			OPTIONAL { ?cds skos:definition ?definition }
			BIND( CONCAT(COALESCE(?definition,"")," (*",?notation,"*)") AS ?capitoloDescription)		
		} GROUP BY ?azione
	}
	
	BIND( COALESCE(?azioneEditorialNote , "") AS ?azioneExtraNotes)
	BIND( CONCAT( 
		COALESCE(?programmaDefinition, ""),
		COALESCE(CONCAT("\n\n",?programmaEditorialNote) ,"")
	) AS ?programmaDescription)
	BIND( COALESCE(?missioneDescription , "N.A.") AS ?missioneDescription)
	BIND( COALESCE(?capitoliList , "N.A.") AS ?capitoliDescription)
	BIND ( STRDT(CONCAT(
		?azioneExtraNotes ,
		"\n\n**Programma**:\n\n", ?programmaDescription ,
		"\n\n**Missione**:\n\n", ?missioneDescription ,
		"\n\n**Capitoli di spesa**:\n\n- ", ?capitoliDescription ,
		"\n\n[Maggiori informazioni...](",STR(?azione),")"
	), bgo:MDString) AS ?abstract )
	
	
}

#trust=1.00	