##################################
# Test fr-ap Facts constraints
################################## 
PREFIX fr: <http://linkeddata.center/botk-fr/v1#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sdmx-attribute:	<http://purl.org/linked-data/sdmx/2009/attribute#> 

ASK {
    ?obs a fr:Fact.

    # Every fr:Fact has exactly one associated financial report.
    {
        ?obs qb:dataSet ?dataset1.
        ?dataset1 a fr:FinancialReport.
        FILTER NOT EXISTS { ?obs qb:dataSet ?dataset2 FILTER (?dataset1 != ?dataset2) }
    }
    
    # Every fr:Fact has exactly one associated valid reference period.   
    {
        ?obs fr:refPeriod ?refPeriod1.
        ?refPeriod1 time:hasBeginning ?begins; time:hasEnd ?ends
        FILTER( ?begins != ?ends )
        FILTER NOT EXISTS { ?obs fr:refPeriod ?refPeriod2 FILTER (?refPeriod1 != ?refPeriod2) }
    }
    

    # Every fr:Fact has exactly one fr:concept    
    {
        ?obs fr:concept ?concept1.
        ?concept1 a skos:Concept.
        FILTER NOT EXISTS { ?obs fr:concept ?concept1 FILTER (?concept1 != ?concept2) }
    }
    
 
    # Every fr:Fact does not have  an unitMeasure attribute or it is the same of the dataset one.    
    {
        {
            FILTER NOT EXISTS { ?obs sdmx-attribute:unitMeasure ?unit1 . }
        } UNION {
            ?obs sdmx-attribute:unitMeasure ?unit1.
            ?dataset1 sdmx-attribute:unitMeasure ?unit1.
        }
    }
}
    