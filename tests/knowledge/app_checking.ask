PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX fr: <http://linkeddata.center/botk-fr/v1#>
PREFIX mef: <http://w3id.org/g0v/it/mef#> 
PREFIX bgo: <http://linkeddata.center/lodmap-bgo/v1#> 
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX qb: <http://purl.org/linked-data/cube#> 

ASK {
 
  	?domain  a bgo:Domain ;
     	dct:source ?report ;
        bgo:title [] ;
        bgo:description [];
  		bgo:hasOverview/bgo:hasPartitions/bgo:hasPartition/bgo:hasAccountSubSet/bgo:hasAccount ?account ;
  		bgo:hasTableView [] ;
    	bgo:hasCredits [] ;
    	bgo:hasTerms [] ;
    	bgo:hasAccountView  [] ;
    .

	# Just one bgo:Domain exists
    FILTER NOT EXISTS { ?domain2 a bgo:Domain FILTER (?domain != ?domain2) }
    
    ?report a mef:FinancialReport ;
    	fr:refPeriod/time:hasBeginning/time:inXSDDateTime ?refdate ;
    	skos:hasTopConcept ?ministero
    .
    
  
    ?account a bgo:Account ;
        bgo:title [] ;
        bgo:description [];
        bgo:amount [] ;
        dct:source ?azione ;
        bgo:hasBreakdown ?breakdown ;
    .
  
    ?azione a fr:Component, mef:Azione ;
       	skos:narrower ?fact ;
    	qb:dataSet ?report ;
        skos:broader/skos:broader/skos:broader ?ministero
  	.
  
  	?fact a fr:Fact, mef:Capitolo ;
    	fr:amount ?factAmount
    .
            
    ?breakdown bgo:title [] ;
        bgo:amount ?factAmount
    .
  
 }   